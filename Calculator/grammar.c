non-terminal    GRAMMAR, NT_DEC, NT_LIST, NT_TAIL, T_DEC, T_LIST, T_TAIL, R_LIST, RULE, RIGHT_SEC, RIGHT_SUB, RIGHT_TAIL, RULE_TAIL, A;
terminal        'non_term', 'term', 'axiom', ';', '|', '::=', ',', 'epsilon', 'non-terminal', 'terminal', 'end_of_program';
GRAMMAR     ::= NT_DEC T_DEC R_LIST A 'end_of_program';
NT_DEC      ::= 'non-terminal' NT_LIST ';' ;
NT_LIST     ::= 'non_term' NT_TAIL;
NT_TAIL     ::= ',' 'non_term' NT_TAIL | epsilon;
T_DEC       ::= 'terminal' T_LIST ';' ;
T_LIST      ::= 'term' T_TAIL;
T_TAIL      ::= ',' 'term' T_TAIL | epsilon;
R_LIST      ::= RULE RULE_TAIL;
RULE        ::= 'non_term' '::=' RIGHT_SEC RIGHT_TAIL ';' ;
RIGHT_SEC   ::= 'non_term' RIGHT_SUB | 'term' RIGHT_SUB | 'epsilon';
RIGHT_SUB   ::= 'non_term' RIGHT_SUB | 'term' RIGHT_SUB | 'epsilon' | epsilon;
RIGHT_TAIL  ::= '|' RIGHT_SEC RIGHT_TAIL | epsilon;
RULE_TAIL   ::= RULE RULE_TAIL | epsilon;
A           ::= 'axiom' 'non_term' ';' ;
axiom       GRAMMAR;